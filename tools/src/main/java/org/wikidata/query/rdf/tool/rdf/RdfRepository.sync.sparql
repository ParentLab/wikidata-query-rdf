# Clear out of date site links
DELETE {
  ?s ?p ?o .
}
WHERE {
  ?s %schema:about% %entity:id% .
  ?s ?p ?o .
  # This construct is constantly reused throughout the updates.  Its job is to not delete statements
  # that are still in use.
  MINUS {
    VALUES ( ?s ?p ?o ) {
      %aboutStatements%
    }
  }
};
# Clear out of date statements about statements
DELETE {
  ?s ?p ?o .
}
WHERE {
  VALUES ?entity {
    %entity:id% %lexemeIds%
  }
  ?entity ?statementPred ?s .
  FILTER( STRSTARTS(STR(?s), "%uris.statement%") ) .
  ?s ?p ?o .
  MINUS {
    VALUES ( ?s ?p ?o ) {
      %statementStatements%
    }
  }
};
# Clear out of date statements about the entity
DELETE {
  ?entity ?p ?o .
}
WHERE {
  VALUES ?entity {
    %entity:id% %lexemeIds%
  }
  ?entity ?p ?o .
  MINUS {
    VALUES ( ?s ?p ?o ) {
      %entityStatements%
    }
  }
};
# Insert new data
INSERT {
  %insertStatements%
} WHERE {};
%cleanupQuery%
